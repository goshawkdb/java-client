apply plugin: 'java'
apply plugin: 'java-library-distribution'
apply plugin: 'jacoco'
apply plugin: 'maven'

version = 'dev'
group = 'io.goshawkdb'
archivesBaseName = "goshawkdb-client"

sourceCompatibility = 1.8
targetCompatibility = 1.8

javadoc {
    source = {
        fileTree(dir: 'src/main/java/io/goshawkdb/client',
                includes: ['Certs.java', 'Connection.java', 'ConnectionFactory.java',
                           'GoshawkObj.java', 'Transaction.java', 'TransactionFun.java',
                           'TxnId.java', 'VarUUId.java'])
    }
}

jar {
    manifest {
        from('MANIFEST.MF')
        attributes("Specification-Version": version,
                "Implementation-Version": version)
    }
    into('META-INF') {
        from('LICENSE')
    }
}

distributions {
    main {
        contents {
            from('LICENSE')
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://localhost/tmp/myRepo/")
            pom.project {
                name 'GoshawkDB Java Client'
                packaging 'jar'
                description 'The Java Client for GoshawkDB'
                url = 'https://goshawkdb.io/'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        name = 'Matthew Sackman'
                        email = 'matthew@goshawkdb.io'
                        organization = 'GoshawkDB'
                        organizationUrl = 'https://goshawkdb.io/'
                    }
                }

                scm {
                    connection 'scm:hg:https://src.goshawkdb.io/java-client/'
                    developerConnection 'scm:hg:https://src.goshawkdb.io/java-client/'
                    url 'https://src.goshawkdb.io/java-client/'
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    manifest {
        from jar.manifest
    }
    into('META-INF') {
        from('LICENSE')
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest {
        from jar.manifest
    }
    into('META-INF') {
        from('LICENSE')
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

test {
    maxParallelForks = 1
    environment(
            'GOSHAWKDB_DEFAULT_CLUSTER_HOSTS': 'localhost',
            'GOSHAWKDB_DEFAULT_CLUSTER_CERT': projectDir.path.concat('/src/test/resources/defaultClusterCert.pem'),
            'GOSHAWKDB_DEFAULT_CLIENT_KEYPAIR': projectDir.path.concat('/src/test/resources/defaultClientKeyPair.pem')
    )
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.capnproto', name: 'runtime', version: '0.1.1'
    compile group: 'io.netty', name: 'netty-all', version: '4.1.0.Final'
    compile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.54'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.10'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
